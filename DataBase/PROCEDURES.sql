USE BD_SALUDCONNECT;
GO


/* ============================================
   PROCEDURES SESSION
   ============================================ */

IF OBJECT_ID('SP_LOGIN_USER', 'P') IS NOT NULL
    DROP PROCEDURE SP_LOGIN_USER;
GO

CREATE PROCEDURE SP_LOGIN_USER
    @EMAIL VARCHAR(100),
    @PASSWORD VARCHAR(255)
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        U.ID_USER,
        U.FIRST_NAME,
        U.LAST_NAME_PAT,
        U.LAST_NAME_MAT,
        U.DOCUMENT,
        U.EMAIL,
        U.BIRTHDATE,
        U.GENDER,
        U.PHONE,
        U.ID_ROLE,
		U.PROFILE_PICTURE,
        R.NAME_ROLE
    FROM TB_USERS U
    INNER JOIN TB_ROLES R ON U.ID_ROLE = R.ID_ROLE
    WHERE U.EMAIL = @EMAIL AND U.PASSWORD_HASH = @PASSWORD AND U.FLG_DELETE = 0;

    IF EXISTS (
        SELECT 1
        FROM TB_USERS U
        WHERE U.EMAIL = @EMAIL AND U.PASSWORD_HASH = @PASSWORD AND U.FLG_DELETE = 0 AND U.ID_ROLE = 3
    )
    BEGIN
        SELECT 
            DS.ID_SPECIALTY,
            DS.YEARS_EXPERIENCE,
            S.NAME_SPECIALTY
        FROM TB_USERS U
        INNER JOIN TB_DOCTOR_SPECIALTIES DS ON U.ID_USER = DS.ID_DOCTOR
        INNER JOIN TB_SPECIALTIES S ON DS.ID_SPECIALTY = S.ID_SPECIALTY
        WHERE U.EMAIL = @EMAIL AND U.PASSWORD_HASH = @PASSWORD AND U.FLG_DELETE = 0;
    END
END
GO

IF OBJECT_ID('SP_REGISTER_PATIENTS', 'P') IS NOT NULL
    DROP PROCEDURE SP_REGISTER_PATIENTS;
GO

CREATE PROCEDURE SP_REGISTER_PATIENTS
    @FIRST_NAME VARCHAR(50),
    @LAST_NAME_PAT VARCHAR(50),
    @LAST_NAME_MAT VARCHAR(50),
    @DOCUMENT VARCHAR(9),
    @BIRTHDATE DATE,
    @PHONE VARCHAR(13),
    @GENDER CHAR(1),
    @EMAIL VARCHAR(100),
    @PASSWORD_HASH VARCHAR(255)
AS
BEGIN
    SET NOCOUNT ON;

	DECLARE @default_image_profile VARCHAR(150) = 'https://res.cloudinary.com/dheqy208f/image/upload/v1760897993/SaludConnect/Patient/no_image_wjvdlr.png';

    IF EXISTS (SELECT 1 FROM TB_USERS WHERE DOCUMENT = @DOCUMENT)
    BEGIN
        RAISERROR('El n�mero de documento %s ya existe', 16, 1, @DOCUMENT);
        RETURN;
    END

    IF EXISTS (SELECT 1 FROM TB_USERS WHERE EMAIL = @EMAIL)
    BEGIN
        RAISERROR('El correo electr�nico %s ya existe', 16, 1, @EMAIL);
        RETURN;
    END

    INSERT INTO TB_USERS(FIRST_NAME, LAST_NAME_PAT, LAST_NAME_MAT,
	DOCUMENT, BIRTHDATE, PHONE, GENDER, EMAIL, PASSWORD_HASH, ID_ROLE,DATE_REGISTER,PROFILE_PICTURE)
    VALUES(@FIRST_NAME, @LAST_NAME_PAT, @LAST_NAME_MAT, @DOCUMENT, @BIRTHDATE, @PHONE, @GENDER, @EMAIL, @PASSWORD_HASH, 1, GETDATE(),@default_image_profile);

    SELECT SCOPE_IDENTITY() AS ID_USER;
END
GO

IF OBJECT_ID('SP_UPDATE_PROFILE_DOCTOR', 'P') IS NOT NULL
    DROP PROCEDURE SP_UPDATE_PROFILE_DOCTOR;
GO

CREATE PROCEDURE SP_UPDATE_PROFILE_DOCTOR
	@ID_USER INT,
    @Email VARCHAR(100) = NULL,
    @ProfilePicture VARCHAR(200) = NULL,
    @PasswordHash VARCHAR(255) = NULL
AS
BEGIN
    SET NOCOUNT ON;
    UPDATE TB_USERS
    SET
        Email = CASE WHEN @Email IS NOT NULL AND @Email <> '' THEN @Email ELSE Email END,
		Profile_Picture = 
    CASE 
        WHEN @ProfilePicture IS NOT NULL AND @ProfilePicture <> '' THEN @ProfilePicture
        WHEN @ProfilePicture = '' THEN NULL
        ELSE Profile_Picture
    END,
        Password_Hash = CASE WHEN @PasswordHash IS NOT NULL AND @PasswordHash <> '' THEN @PasswordHash ELSE Password_Hash END
    WHERE ID_USER = @ID_USER;
END
GO

/* ============================================
   PROCEDURES MEDICAL RECORDS
   ============================================ */
IF OBJECT_ID('SP_INSERT_RECORD_WITH_SERVICES', 'P') IS NOT NULL
    DROP PROCEDURE SP_INSERT_RECORD_WITH_SERVICES;
GO

CREATE PROCEDURE SP_INSERT_RECORD_WITH_SERVICES
    @IdAppointment INT,
    @Observations VARCHAR(2000),
    @Diagnosis VARCHAR(500) = NULL,
    @Treatment VARCHAR(2000) = NULL,
    @ServiceIds VARCHAR(MAX) = NULL,
    @NewRecordID INT OUTPUT
AS
BEGIN
    SET NOCOUNT ON;
    BEGIN TRY
        BEGIN TRANSACTION;

        INSERT INTO TB_MEDICAL_RECORDS (ID_APPOINTMENT, OBSERVATIONS, DIAGNOSIS, TREATMENT)
        VALUES (@IdAppointment, @Observations, @Diagnosis, @Treatment);

        SET @NewRecordID = SCOPE_IDENTITY();

        IF @ServiceIds IS NOT NULL AND LTRIM(RTRIM(@ServiceIds)) <> ''
        BEGIN
            INSERT INTO TB_ADDITIONAL_SERVICES (ID_RECORD, ID_SERVICE, PRICE_AT_TIME, STATE)
            SELECT
                @NewRecordID,
                TRY_CAST(value AS INT),
                S.PRICE,
                'P' 
            FROM STRING_SPLIT(@ServiceIds, ',') AS split
            INNER JOIN TB_SERVICES S ON S.ID_SERVICE = TRY_CAST(split.value AS INT)
            WHERE TRY_CAST(split.value AS INT) IS NOT NULL;
        END
        COMMIT;
    END TRY
    BEGIN CATCH
        ROLLBACK;
        THROW;
    END CATCH
	END
GO
/* ============================================
   PROCEDURES SPECIALTY
   ============================================ */
IF OBJECT_ID('SP_LIST_SPECIALTIES', 'P') IS NOT NULL
    DROP PROCEDURE SP_LIST_SPECIALTIES;
GO

CREATE PROCEDURE SP_LIST_SPECIALTIES
AS
BEGIN
	SELECT ID_SPECIALTY,NAME_SPECIALTY FROM TB_SPECIALTIES
END
GO
/* ============================================
   PROCEDURES SERVICES
   ============================================ */

IF OBJECT_ID('SP_LIST_SERVICES_FOR_DOCTOR', 'P') IS NOT NULL
    DROP PROCEDURE SP_LIST_SERVICES_FOR_DOCTOR;
GO

CREATE PROCEDURE SP_LIST_SERVICES_FOR_DOCTOR
    @IDDOCTOR INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        S.ID_SERVICE,
        S.NAME_SERVICE,
        S.DESCRIPTION,
        S.PRICE,
        S.DURATION_MINUTES, 
        SPE.ID_SPECIALTY, 
        SPE.NAME_SPECIALTY 
    FROM TB_SERVICES S
    INNER JOIN TB_SPECIALTIES SPE ON S.ID_SPECIALTY = SPE.ID_SPECIALTY
    INNER JOIN TB_DOCTOR_SPECIALTIES D ON D.ID_SPECIALTY = S.ID_SPECIALTY
    WHERE D.ID_DOCTOR = @IDDOCTOR AND S.FLG_DELETE = 0
    ORDER BY SPE.NAME_SPECIALTY, S.NAME_SERVICE;
END
GO

IF OBJECT_ID('SP_TOTAL_SERVICES', 'P') IS NOT NULL
    DROP PROCEDURE SP_TOTAL_SERVICES;
GO

CREATE PROCEDURE SP_TOTAL_SERVICES
AS
BEGIN
    SET NOCOUNT ON;

    SELECT COUNT(*) AS totalServices
    FROM TB_SERVICES 
    WHERE FLG_DELETE = 0;
END
GO

IF OBJECT_ID('SP_MIN_SERVICE', 'P') IS NOT NULL
    DROP PROCEDURE SP_MIN_SERVICE;
GO

CREATE PROCEDURE SP_MIN_SERVICE
AS
BEGIN
    SET NOCOUNT ON;

    SELECT MIN(DURATION_MINUTES) AS MinDuration
    FROM TB_SERVICES
    WHERE FLG_DELETE = 0;
END
GO

IF OBJECT_ID('SP_LIST_SERVICES_BY_SPECIALTY', 'P') IS NOT NULL
    DROP PROCEDURE SP_LIST_SERVICES_BY_SPECIALTY;
GO

CREATE PROCEDURE SP_LIST_SERVICES_BY_SPECIALTY
    @IdSpecialty INT = NULL 
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        S.ID_SERVICE, 
        S.NAME_SERVICE, 
        S.DESCRIPTION, 
        S.PRICE, 
        S.DURATION_MINUTES,
        SP.ID_SPECIALTY,
        SP.NAME_SPECIALTY
    FROM TB_SERVICES S
    INNER JOIN TB_SPECIALTIES SP ON S.ID_SPECIALTY = SP.ID_SPECIALTY
    WHERE (@IdSpecialty IS NULL OR S.ID_SPECIALTY = @IdSpecialty);
END
GO

/* ============================================
   PROCEDURES APPOINTMENTS
   ============================================ */

IF OBJECT_ID('SP_APPOINTMENTS_TODAY_FOR_DOCTOR', 'P') IS NOT NULL
    DROP PROCEDURE SP_APPOINTMENTS_TODAY_FOR_DOCTOR;
GO

CREATE PROCEDURE SP_APPOINTMENTS_TODAY_FOR_DOCTOR
    @IDDOCTOR INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT COUNT(*) AS AppointmentsToday
    FROM TB_APPOINTMENTS
    WHERE 
        ID_DOCTOR = @IDDOCTOR
        AND CAST(DATE_APPOINTMENT AS DATE) = CAST(GETDATE() AS DATE);
END
GO

IF OBJECT_ID('SP_TOTAL_PATIENTS_FOR_DOCTOR', 'P') IS NOT NULL
    DROP PROCEDURE SP_TOTAL_PATIENTS_FOR_DOCTOR;
GO

CREATE PROCEDURE SP_TOTAL_PATIENTS_FOR_DOCTOR
    @IDDOCTOR INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT COUNT(DISTINCT ID_PATIENT) AS TotalUniquePatients
    FROM TB_APPOINTMENTS
    WHERE ID_DOCTOR = @IDDOCTOR;
END
GO

IF OBJECT_ID('SP_TOTAL_APPOINTMENTS_COMPLETED', 'P') IS NOT NULL
    DROP PROCEDURE SP_TOTAL_APPOINTMENTS_COMPLETED;
GO

CREATE PROCEDURE SP_TOTAL_APPOINTMENTS_COMPLETED
    @IDDOCTOR INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT COUNT(*) AS CompletedAppointments
    FROM TB_APPOINTMENTS
    WHERE ID_DOCTOR = @IDDOCTOR AND STATE = 'A';
END
GO

IF OBJECT_ID('SP_COUNT_UPCOMMING_APPOINTMENTS_FOR_DOCTOR', 'P') IS NOT NULL
    DROP PROCEDURE SP_COUNT_UPCOMMING_APPOINTMENTS_FOR_DOCTOR;
GO

CREATE PROCEDURE SP_COUNT_UPCOMMING_APPOINTMENTS_FOR_DOCTOR
    @IDDOCTOR INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT COUNT(*) AS UpcomingAppointments
    FROM TB_APPOINTMENTS
    WHERE 
        ID_DOCTOR = @IDDOCTOR 
        AND DATE_APPOINTMENT > GETDATE()
        AND STATE = 'P'; 
END
GO

IF OBJECT_ID('SP_LIST_APPOINTMENTS_BY_DATE_FOR_DOCTOR', 'P') IS NOT NULL
    DROP PROCEDURE SP_LIST_APPOINTMENTS_BY_DATE_FOR_DOCTOR;
GO

CREATE PROCEDURE SP_LIST_APPOINTMENTS_BY_DATE_FOR_DOCTOR
    @DoctorId INT,
    @Date DATE = NULL
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        A.ID_APPOINTMENT,
        P.ID_USER AS PatientId,
        P.FIRST_NAME,
        P.LAST_NAME_PAT,
        P.LAST_NAME_MAT,
        CAST(A.DATE_APPOINTMENT AS DATE) AS DateOnly,
        CONVERT(VARCHAR(5), A.DATE_APPOINTMENT, 108) AS AppointmentTime, 
        A.STATE,
        S.NAME_SPECIALTY,
        A.APPOINTMENT_PRICE,
        MR.ID_RECORD 
    FROM TB_APPOINTMENTS A
    INNER JOIN TB_USERS P ON A.ID_PATIENT = P.ID_USER
    INNER JOIN TB_SPECIALTIES S ON A.ID_SPECIALTY = S.ID_SPECIALTY
    LEFT JOIN TB_MEDICAL_RECORDS MR ON A.ID_APPOINTMENT = MR.ID_APPOINTMENT
    WHERE A.ID_DOCTOR = @DoctorId
      AND (@Date IS NULL OR CAST(A.DATE_APPOINTMENT AS DATE) = @Date)
    ORDER BY A.DATE_APPOINTMENT;
END
GO

IF OBJECT_ID('SP_GET_APPOINTMENTS_FOR_7_DAYS', 'P') IS NOT NULL
    DROP PROCEDURE SP_GET_APPOINTMENTS_FOR_7_DAYS;
GO

CREATE PROCEDURE SP_GET_APPOINTMENTS_FOR_7_DAYS
    @DoctorId INT = NULL
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        CAST(DATE_APPOINTMENT AS DATE) AS AppointmentDate,
        COUNT(*) AS TotalAppointments
    FROM TB_APPOINTMENTS
    WHERE 
        DATE_APPOINTMENT >= CAST(GETDATE() - 6 AS DATE) 
        AND DATE_APPOINTMENT < CAST(GETDATE() + 1 AS DATE) 
        AND STATE IN ('A', 'P', 'X', 'N')
        AND (@DoctorId IS NULL OR ID_DOCTOR = @DoctorId)
    GROUP BY 
        CAST(DATE_APPOINTMENT AS DATE)
    ORDER BY 
        AppointmentDate;
END
GO

IF OBJECT_ID('SP_GET_APPOINTEMNT_FOR_ID', 'P') IS NOT NULL
    DROP PROCEDURE SP_GET_APPOINTEMNT_FOR_ID;
GO

CREATE PROCEDURE SP_GET_APPOINTEMNT_FOR_ID
    @ID_APPOINTMENT INT
AS
BEGIN
    SET NOCOUNT ON;

    -- Informaci�n de la cita
    SELECT 
        A.ID_APPOINTMENT,
        P.ID_USER AS PatientId,
        P.FIRST_NAME,
        P.LAST_NAME_PAT,
        P.LAST_NAME_MAT,
        P.DOCUMENT,
        P.BIRTHDATE,
        P.PHONE,
        P.GENDER,
        P.EMAIL,
        CAST(A.DATE_APPOINTMENT AS DATE) AS DateOnly,
        CONVERT(VARCHAR(5), A.DATE_APPOINTMENT, 108) AS AppointmentTime, 
        A.STATE,
        S.ID_SPECIALTY,
        S.NAME_SPECIALTY,
        A.APPOINTMENT_PRICE,
        C.NUMBER_CONSULTORIES,
        C.FLOOR_NUMBER,
        D.ID_USER AS DoctorId,
        D.FIRST_NAME AS DoctorFirstName,
        D.LAST_NAME_PAT AS DoctorLastNamePat,
        D.LAST_NAME_MAT AS DoctorLastNameMat,
        -- Informaci�n del registro m�dico si existe
        MR.ID_RECORD,
        MR.DATE_REPORT,
        MR.OBSERVATIONS,
        MR.DIAGNOSIS,
        MR.TREATMENT
    FROM TB_APPOINTMENTS A
    INNER JOIN TB_USERS P ON A.ID_PATIENT = P.ID_USER
    INNER JOIN TB_USERS D ON A.ID_DOCTOR = D.ID_USER
    INNER JOIN TB_SPECIALTIES S ON A.ID_SPECIALTY = S.ID_SPECIALTY
    INNER JOIN TB_CONSULTORIES C ON A.ID_CONSULTORIES = C.ID_CONSULTORIES
    LEFT JOIN TB_MEDICAL_RECORDS MR ON A.ID_APPOINTMENT = MR.ID_APPOINTMENT
    WHERE A.ID_APPOINTMENT = @ID_APPOINTMENT;

    -- Si existe registro m�dico, traer tambi�n los servicios adicionales
    IF EXISTS (SELECT 1 FROM TB_MEDICAL_RECORDS WHERE ID_APPOINTMENT = @ID_APPOINTMENT)
    BEGIN
        SELECT 
            ADS.ID_ADD_SERVICE,
            ADS.ID_RECORD,
            ADS.ID_SERVICE,
            ADS.STATE,
            S.NAME_SERVICE,
            S.DESCRIPTION,
            S.PRICE,
            S.DURATION_MINUTES,
            SPE.NAME_SPECIALTY
        FROM TB_MEDICAL_RECORDS MR
        INNER JOIN TB_ADDITIONAL_SERVICES ADS ON MR.ID_RECORD = ADS.ID_RECORD
        INNER JOIN TB_SERVICES S ON ADS.ID_SERVICE = S.ID_SERVICE
        INNER JOIN TB_SPECIALTIES SPE ON S.ID_SPECIALTY = SPE.ID_SPECIALTY
        WHERE MR.ID_APPOINTMENT = @ID_APPOINTMENT
        ORDER BY ADS.ID_ADD_SERVICE;
    END
END
GO

IF OBJECT_ID('SP_APPOINTMENT_CHANGE_STATE', 'P') IS NOT NULL
    DROP PROCEDURE SP_APPOINTMENT_CHANGE_STATE;
GO

CREATE PROC SP_APPOINTMENT_CHANGE_STATE
	@IDAPPOINTMENT INT,
	@STATE CHAR(1)
AS
BEGIN
	UPDATE TB_APPOINTMENTS
	SET STATE = @STATE
	WHERE ID_APPOINTMENT = @IDAPPOINTMENT
END
GO

/* ============================================
   PROCEDURES SPECIALTIES
   ============================================ */

IF OBJECT_ID('SP_TOTAL_SPECIALTIES', 'P') IS NOT NULL
    DROP PROCEDURE SP_TOTAL_SPECIALTIES;
GO

CREATE PROCEDURE SP_TOTAL_SPECIALTIES
AS
BEGIN
    SET NOCOUNT ON;

    SELECT COUNT(*) AS totalSpecialties
    FROM TB_SPECIALTIES 
    WHERE FLG_DELETE = 0;
END
GO

/* ============================================
   PROCEDURES DOCTOR
   ============================================ */
IF OBJECT_ID('sp_update_information_patient', 'P') IS NOT NULL
    DROP PROCEDURE sp_update_information_patient;
GO

CREATE PROCEDURE sp_update_information_patient 
    -- VARIABLES USER
    @idUser INT, 
    @firstname VARCHAR(50),
    @lastNamePat VARCHAR(50),
    @lastNameMat VARCHAR(50),
    @document VARCHAR(50),
    @phone VARCHAR(50),
    @email VARCHAR(50),
    @profilePicture VARCHAR(150) = '',
    -- VARIABLES CONTACTO
    @idContact INT,
    @namesContact VARCHAR(50) = '',
    @contacNamePat VARCHAR(50) = '',
    @contacNameMat VARCHAR(50) = '',
    @idRelation INT,
    @phoneEmergency VARCHAR(50) = ''	
AS
BEGIN
    SET NOCOUNT ON;

    BEGIN TRY
        BEGIN TRANSACTION
                    
            UPDATE TB_USERS
            SET FIRST_NAME = @firstname,
                LAST_NAME_PAT = @lastNamePat, 
                LAST_NAME_MAT = @lastNameMat,
                DOCUMENT = @document,
                PHONE = @phone,
                EMAIL = @email,
                PROFILE_PICTURE = @profilePicture 
            WHERE ID_USER = @idUser;

            IF @idContact IS NOT NULL
            BEGIN
                UPDATE TB_EMERGENCY_CONTACT
                SET NAMES_CONTACT = @namesContact,
                    LAST_NAME_PAT = @contacNamePat,
                    LAST_NAME_MAT = @contacNameMat,
                    ID_RELATIONSHIP = @idRelation,
                    PHONE_EMERGENCY = @phoneEmergency
                WHERE ID_E_CONTACT = @idContact;
            END

        COMMIT TRANSACTION;
    END TRY
    BEGIN CATCH
        ROLLBACK TRANSACTION;
        THROW;
    END CATCH
END
GO

IF OBJECT_ID('SP_TOTAL_DOCTOR', 'P') IS NOT NULL
    DROP PROCEDURE SP_TOTAL_DOCTOR;
GO

CREATE PROCEDURE SP_TOTAL_DOCTOR
AS
BEGIN
    SET NOCOUNT ON;

    SELECT COUNT(*) AS totalDoctors
    FROM TB_USERS
    WHERE ID_ROLE = 3 AND FLG_DELETE = 0;
END
GO

IF OBJECT_ID('sp_patient_information', 'P') IS NOT NULL
    DROP PROCEDURE sp_patient_information;
GO

create proc sp_patient_information
@idUser int
as
begin
	select 
	us.ID_USER, us.FIRST_NAME, us.LAST_NAME_PAT, us.LAST_NAME_MAT,us.DOCUMENT,
	us.PHONE, us.EMAIL,us.DATE_REGISTER,us.PROFILE_PICTURE,
	ec.NAMES_CONTACT, ec.LAST_NAME_PAT, ec.LAST_NAME_MAT,
	ec.ID_RELATIONSHIP, ec.PHONE_EMERGENCY, rl.DESCRIPTION_RELATIONSHIP
	from TB_USERS us 
	INNER JOIN TB_EMERGENCY_CONTACT ec on us.ID_E_CONTACT = ec.ID_E_CONTACT
	INNER JOIN TB_RELATIONSHIP rl on ec.ID_RELATIONSHIP = rl.ID_RELATIONSHIP
	where ID_USER = @idUser
end
go

IF OBJECT_ID('sp_historial_appointments', 'P') IS NOT NULL
    DROP PROCEDURE sp_historial_appointments;
GO

CREATE PROC sp_historial_appointments
@idPatient int 

as 
begin
	
	SELECT 
		ap.ID_APPOINTMENT,
		CONCAT(md.FIRST_NAME , ' ' , md.LAST_NAME_PAT) AS NombresDoctor,
		md.GENDER,
		md.PROFILE_PICTURE,
		s.ID_SPECIALTY,
		s.NAME_SPECIALTY,
		FORMAT(ap.DATE_APPOINTMENT, 'HH:mm') AS Hora_Cita,
		FORMAT(ap.DATE_APPOINTMENT, 'dd/MM/yyyy') AS fecha_cita,
		sv.NAME_SERVICE,
		sv.DESCRIPTION,
		sv.DURATION_MINUTES,
		cs.NUMBER_CONSULTORIES,
		ap.STATE,
		ap.APPOINTMENT_PRICE,
		mds.DIAGNOSIS, mds.OBSERVATIONS, mds.TREATMENT
	FROM TB_APPOINTMENTS ap
	INNER JOIN TB_USERS md ON ap.ID_DOCTOR = md.ID_USER
	INNER JOIN TB_SPECIALTIES s ON ap.ID_SPECIALTY = s.ID_SPECIALTY
	INNER JOIN TB_SERVICES sv ON sv.ID_SERVICE = (
		SELECT TOP 1 sv2.ID_SERVICE
		FROM TB_SERVICES sv2
		WHERE sv2.ID_SPECIALTY = s.ID_SPECIALTY
		ORDER BY sv2.ID_SERVICE
	)
	INNER JOIN TB_CONSULTORIES cs ON ap.ID_CONSULTORIES = cs.ID_CONSULTORIES
	INNER JOIN TB_MEDICAL_RECORDS mds ON mds.ID_APPOINTMENT = ap.ID_APPOINTMENT 
	WHERE ap.ID_PATIENT = @idPatient 
end
go


exec sp_historial_appointments 2


/* ============================================
   PROCEDURES USER
   ============================================ */

IF OBJECT_ID('SP_GET_USER_PROFILE', 'P') IS NOT NULL
    DROP PROCEDURE SP_GET_USER_PROFILE;
GO

CREATE PROCEDURE SP_GET_USER_PROFILE
    @ID_USER INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        U.ID_USER,
        U.FIRST_NAME,
        U.LAST_NAME_PAT,
        U.LAST_NAME_MAT,
        U.DOCUMENT,
        U.BIRTHDATE,
        U.PHONE,
        U.GENDER,
        U.EMAIL,
        U.ID_ROLE,
        R.NAME_ROLE,
        U.PROFILE_PICTURE,
        U.DATE_REGISTER,
        EC.NAMES_CONTACT,
        EC.LAST_NAME_PAT AS EmergencyLastNamePat,
        EC.LAST_NAME_MAT AS EmergencyLastNameMat,
        EC.PHONE_EMERGENCY,
        REL.DESCRIPTION_RELATIONSHIP
    FROM TB_USERS U
    INNER JOIN TB_ROLES R ON U.ID_ROLE = R.ID_ROLE
    LEFT JOIN TB_EMERGENCY_CONTACT EC ON U.ID_E_CONTACT = EC.ID_E_CONTACT
    LEFT JOIN TB_RELATIONSHIP REL ON EC.ID_RELATIONSHIP = REL.ID_RELATIONSHIP
    WHERE U.ID_USER = @ID_USER AND U.FLG_DELETE = 0;

    IF EXISTS (SELECT 1 FROM TB_USERS WHERE ID_USER = @ID_USER AND ID_ROLE = 3 AND FLG_DELETE = 0)
    BEGIN
        SELECT 
            DS.ID_SPECIALTY,
            S.NAME_SPECIALTY,
            DS.YEARS_EXPERIENCE
        FROM TB_DOCTOR_SPECIALTIES DS
        INNER JOIN TB_SPECIALTIES S ON DS.ID_SPECIALTY = S.ID_SPECIALTY
        WHERE DS.ID_DOCTOR = @ID_USER;
    END
END
GO

/* ============================================
   PROCEDURES ADMIN
   ============================================ */

IF OBJECT_ID('SP_Total_Appointments', 'P') IS NOT NULL
    DROP PROCEDURE SP_Total_Appointments;
GO

CREATE PROCEDURE SP_Total_Appointments
AS
BEGIN
    SET NOCOUNT ON;

    SELECT COUNT(*) AS TotalAppointments
    FROM TB_APPOINTMENTS;
END
GO

CREATE PROCEDURE SP_GET_TOTAL_PATIENTS
AS
BEGIN
    SELECT COUNT(DISTINCT U.ID_USER) AS TOTAL_PATIENTS
    FROM TB_USERS U
    INNER JOIN TB_ROLES R ON U.ID_ROLE = R.ID_ROLE
    WHERE R.NAME_ROLE = 'PACIENTE' AND U.FLG_DELETE = 0;
END
GO

CREATE PROCEDURE SP_GET_TOTAL_DOCTORS
AS
BEGIN
    SELECT COUNT(DISTINCT U.ID_USER) AS TOTAL_DOCTORS
    FROM TB_USERS U
    INNER JOIN TB_ROLES R ON U.ID_ROLE = R.ID_ROLE
    WHERE R.NAME_ROLE = 'DOCTOR' AND U.FLG_DELETE = 0;
END
GO

CREATE PROCEDURE SP_GET_APPOINTMENTS_BY_STATE
AS
BEGIN
    SELECT 
        CASE STATE
            WHEN 'A' THEN 'Atendida'
            WHEN 'P' THEN 'Pendiente'
            WHEN 'X' THEN 'Cancelada'
            WHEN 'N' THEN 'No Asistió'
        END AS STATE_NAME,
        COUNT(*) AS TOTAL
    FROM TB_APPOINTMENTS
    GROUP BY STATE;
END
GO


CREATE PROCEDURE SP_GET_TODAY_APPOINTMENTS
AS
BEGIN
    SELECT COUNT(*) AS TODAY_APPOINTMENTS
    FROM TB_APPOINTMENTS
    WHERE CAST(DATE_APPOINTMENT AS DATE) = CAST(GETDATE() AS DATE)
    AND STATE IN ('P', 'A');
END
GO

CREATE PROCEDURE SP_GET_TOP_SPECIALTIES
    @TOP INT = 5
AS
BEGIN
    SELECT TOP (@TOP)
        S.NAME_SPECIALTY,
        COUNT(A.ID_APPOINTMENT) AS TOTAL_APPOINTMENTS
    FROM TB_APPOINTMENTS A
    INNER JOIN TB_SPECIALTIES S ON A.ID_SPECIALTY = S.ID_SPECIALTY
    WHERE A.STATE != 'X'
    GROUP BY S.NAME_SPECIALTY
    ORDER BY COUNT(A.ID_APPOINTMENT) DESC;
END
GO

CREATE PROCEDURE SP_GET_ALL_SPECIALTIES
AS
BEGIN
    SELECT 
        ID_SPECIALTY,
        NAME_SPECIALTY,
        DESCRIPTION_SPECIALITY
    FROM TB_SPECIALTIES
    WHERE FLG_DELETE = 0;
END
GO



/*
=====================
CRUDS
=====================
*/

CREATE OR ALTER PROCEDURE SP_LIST_DOCTORS
AS
BEGIN
    SELECT 
        U.ID_USER,
        U.FIRST_NAME,
        U.LAST_NAME_PAT,
        U.LAST_NAME_MAT,
        U.DOCUMENT,
        U.BIRTHDATE,
        U.PHONE,
        U.GENDER,
        U.EMAIL,
        U.DATE_REGISTER,
        U.PROFILE_PICTURE,
        R.NAME_ROLE
    FROM TB_USERS U
    INNER JOIN TB_ROLES R ON U.ID_ROLE = R.ID_ROLE
    WHERE R.ID_ROLE = 3 ;
END
GO


exec SP_LIST_DOCTORS
go


-- Obtener Doctor por ID con sus especialidades
CREATE OR ALTER PROCEDURE SP_GET_DOCTOR_BY_ID
    @ID_DOCTOR INT
AS
BEGIN
    -- Información del doctor
    SELECT 
        U.ID_USER,
        U.FIRST_NAME,
        U.LAST_NAME_PAT,
        U.LAST_NAME_MAT,
        U.DOCUMENT,
        U.BIRTHDATE,
        U.PHONE,
        U.GENDER,
        U.EMAIL,
        U.PROFILE_PICTURE,
        U.DATE_REGISTER
    FROM TB_USERS U
    WHERE U.ID_USER = @ID_DOCTOR AND U.FLG_DELETE = 0;

    -- Especialidades del doctor
    SELECT 
        DS.ID_SPECIALTY,
        S.NAME_SPECIALTY,
        DS.YEARS_EXPERIENCE,
        DS.EXPERIENCE,
        DS.DOC_LANGUAGES
    FROM TB_DOCTOR_SPECIALTIES DS
    INNER JOIN TB_SPECIALTIES S ON DS.ID_SPECIALTY = S.ID_SPECIALTY
    WHERE DS.ID_DOCTOR = @ID_DOCTOR;
END
GO

-- Crear Doctor
CREATE or alter PROCEDURE SP_CREATE_DOCTOR
    @FIRST_NAME VARCHAR(50),
    @LAST_NAME_PAT VARCHAR(50),
    @LAST_NAME_MAT VARCHAR(50),
    @DOCUMENT VARCHAR(9),
    @BIRTHDATE DATE,
    @PHONE VARCHAR(13),
    @GENDER CHAR(1),
    @EMAIL VARCHAR(100),
    @PASSWORD_HASH VARCHAR(255),
    @ID_ROLE INT,
    @PROFILE_PICTURE VARCHAR(MAX) = NULL
AS
BEGIN
    INSERT INTO TB_USERS (
        FIRST_NAME, LAST_NAME_PAT, LAST_NAME_MAT, DOCUMENT,
        BIRTHDATE, PHONE, GENDER, EMAIL, PASSWORD_HASH,
        ID_ROLE, PROFILE_PICTURE
    )
    VALUES (
        @FIRST_NAME, @LAST_NAME_PAT, @LAST_NAME_MAT, @DOCUMENT,
        @BIRTHDATE, @PHONE, @GENDER, @EMAIL, @PASSWORD_HASH,
        3, @PROFILE_PICTURE
    );
    
    SELECT SCOPE_IDENTITY() AS ID_USER;
END
GO

-- Actualizar Doctor
CREATE PROCEDURE SP_UPDATE_DOCTOR
    @ID_USER INT,
    @FIRST_NAME VARCHAR(50),
    @LAST_NAME_PAT VARCHAR(50),
    @LAST_NAME_MAT VARCHAR(50),
    @PHONE VARCHAR(13),
    @EMAIL VARCHAR(100),
    @PROFILE_PICTURE VARCHAR(MAX) = NULL
AS
BEGIN
    UPDATE TB_USERS
    SET 
        FIRST_NAME = @FIRST_NAME,
        LAST_NAME_PAT = @LAST_NAME_PAT,
        LAST_NAME_MAT = @LAST_NAME_MAT,
        PHONE = @PHONE,
        EMAIL = @EMAIL,
        PROFILE_PICTURE = ISNULL(@PROFILE_PICTURE, PROFILE_PICTURE)
    WHERE ID_USER = @ID_USER AND FLG_DELETE = 0;
END
GO

-- Eliminar Doctor (Soft Delete)
CREATE PROCEDURE SP_DELETE_DOCTOR
    @ID_USER INT
AS
BEGIN
    UPDATE TB_USERS
    SET FLG_DELETE = 1
    WHERE ID_USER = @ID_USER;
END
GO

-- Agregar Especialidad a Doctor
CREATE PROCEDURE SP_ADD_DOCTOR_SPECIALTY
    @ID_DOCTOR INT,
    @ID_SPECIALTY INT,
    @YEARS_EXPERIENCE INT,
    @EXPERIENCE VARCHAR(120),
    @DOC_LANGUAGES VARCHAR(100)
AS
BEGIN
    INSERT INTO TB_DOCTOR_SPECIALTIES (
        ID_DOCTOR, ID_SPECIALTY, YEARS_EXPERIENCE, EXPERIENCE, DOC_LANGUAGES
    )
    VALUES (
        @ID_DOCTOR, @ID_SPECIALTY, @YEARS_EXPERIENCE, @EXPERIENCE, @DOC_LANGUAGES
    );
END
GO



-- Listar Servicios
CREATE PROCEDURE SP_LIST_SERVICES
AS
BEGIN
    SELECT 
        S.ID_SERVICE,
        S.NAME_SERVICE,
        S.DESCRIPTION,
        S.PRICE,
        S.DURATION_MINUTES,
        S.ID_SPECIALTY,
        SP.NAME_SPECIALTY
    FROM TB_SERVICES S
    INNER JOIN TB_SPECIALTIES SP ON S.ID_SPECIALTY = SP.ID_SPECIALTY
    WHERE S.FLG_DELETE = 0;
END
GO

-- Obtener Servicio por ID
CREATE PROCEDURE SP_GET_SERVICE_BY_ID
    @ID_SERVICE INT
AS
BEGIN
    SELECT 
        S.ID_SERVICE,
        S.NAME_SERVICE,
        S.DESCRIPTION,
        S.PRICE,
        S.DURATION_MINUTES,
        S.ID_SPECIALTY,
        SP.NAME_SPECIALTY
    FROM TB_SERVICES S
    INNER JOIN TB_SPECIALTIES SP ON S.ID_SPECIALTY = SP.ID_SPECIALTY
    WHERE S.ID_SERVICE = @ID_SERVICE AND S.FLG_DELETE = 0;
END
GO

-- Crear Servicio
CREATE PROCEDURE SP_CREATE_SERVICE
    @NAME_SERVICE VARCHAR(100),
    @DESCRIPTION VARCHAR(500),
    @PRICE DECIMAL(10,2),
    @DURATION_MINUTES INT,
    @ID_SPECIALTY INT
AS
BEGIN
    INSERT INTO TB_SERVICES (
        NAME_SERVICE, DESCRIPTION, PRICE, DURATION_MINUTES, ID_SPECIALTY
    )
    VALUES (
        @NAME_SERVICE, @DESCRIPTION, @PRICE, @DURATION_MINUTES, @ID_SPECIALTY
    );
    
    SELECT SCOPE_IDENTITY() AS ID_SERVICE;
END
GO

-- Actualizar Servicio
CREATE PROCEDURE SP_UPDATE_SERVICE
    @ID_SERVICE INT,
    @NAME_SERVICE VARCHAR(100),
    @DESCRIPTION VARCHAR(500),
    @PRICE DECIMAL(10,2),
    @DURATION_MINUTES INT,
    @ID_SPECIALTY INT
AS
BEGIN
    UPDATE TB_SERVICES
    SET 
        NAME_SERVICE = @NAME_SERVICE,
        DESCRIPTION = @DESCRIPTION,
        PRICE = @PRICE,
        DURATION_MINUTES = @DURATION_MINUTES,
        ID_SPECIALTY = @ID_SPECIALTY
    WHERE ID_SERVICE = @ID_SERVICE AND FLG_DELETE = 0;
END
GO

-- Eliminar Servicio (Soft Delete)
CREATE PROCEDURE SP_DELETE_SERVICE
    @ID_SERVICE INT
AS
BEGIN
    UPDATE TB_SERVICES
    SET FLG_DELETE = 1
    WHERE ID_SERVICE = @ID_SERVICE;
END
GO

-- Listar Consultorios
CREATE PROCEDURE SP_LIST_CONSULTORIES
AS
BEGIN
    SELECT 
        C.ID_CONSULTORIES,
        C.NUMBER_CONSULTORIES,
        C.FLOOR_NUMBER,
        C.ID_SPECIALTY,
        S.NAME_SPECIALTY
    FROM TB_CONSULTORIES C
    INNER JOIN TB_SPECIALTIES S ON C.ID_SPECIALTY = S.ID_SPECIALTY;
END
GO

-- Obtener Consultorio por ID
CREATE PROCEDURE SP_GET_CONSULTORY_BY_ID
    @ID_CONSULTORIES INT
AS
BEGIN
    SELECT 
        C.ID_CONSULTORIES,
        C.NUMBER_CONSULTORIES,
        C.FLOOR_NUMBER,
        C.ID_SPECIALTY,
        S.NAME_SPECIALTY
    FROM TB_CONSULTORIES C
    INNER JOIN TB_SPECIALTIES S ON C.ID_SPECIALTY = S.ID_SPECIALTY
    WHERE C.ID_CONSULTORIES = @ID_CONSULTORIES;
END
GO

-- Crear Consultorio
CREATE PROCEDURE SP_CREATE_CONSULTORY
    @NUMBER_CONSULTORIES VARCHAR(10),
    @FLOOR_NUMBER INT,
    @ID_SPECIALTY INT
AS
BEGIN
    INSERT INTO TB_CONSULTORIES (
        NUMBER_CONSULTORIES, FLOOR_NUMBER, ID_SPECIALTY
    )
    VALUES (
        @NUMBER_CONSULTORIES, @FLOOR_NUMBER, @ID_SPECIALTY
    );
    
    SELECT SCOPE_IDENTITY() AS ID_CONSULTORIES;
END
GO

-- Actualizar Consultorio
CREATE PROCEDURE SP_UPDATE_CONSULTORY
    @ID_CONSULTORIES INT,
    @NUMBER_CONSULTORIES VARCHAR(10),
    @FLOOR_NUMBER INT,
    @ID_SPECIALTY INT
AS
BEGIN
    UPDATE TB_CONSULTORIES
    SET 
        NUMBER_CONSULTORIES = @NUMBER_CONSULTORIES,
        FLOOR_NUMBER = @FLOOR_NUMBER,
        ID_SPECIALTY = @ID_SPECIALTY
    WHERE ID_CONSULTORIES = @ID_CONSULTORIES;
END
GO

-- Eliminar Consultorio
CREATE PROCEDURE SP_DELETE_CONSULTORY
    @ID_CONSULTORIES INT
AS
BEGIN
    DELETE FROM TB_CONSULTORIES
    WHERE ID_CONSULTORIES = @ID_CONSULTORIES;
END
GO



/* ============================================
   PROCEDURES PACIENTE-CLIENTE
   Estados: A=Atendido, P=Pendiente, X=Cancelado, N=No asisti�
   ============================================ */

IF OBJECT_ID('sp_total_citas', 'P') IS NOT NULL
    DROP PROCEDURE sp_total_citas;
GO

CREATE PROCEDURE sp_total_citas
    @idPaciente INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT COUNT(*) AS Total_Citas 
    FROM TB_APPOINTMENTS
    WHERE ID_PATIENT = @idPaciente;
END
GO

IF OBJECT_ID('sp_total_citas_asistidas', 'P') IS NOT NULL
    DROP PROCEDURE sp_total_citas_asistidas;
GO

CREATE PROCEDURE sp_total_citas_asistidas
    @idPaciente INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT COUNT(*) AS Citas_Asistidas 
    FROM TB_APPOINTMENTS
    WHERE ID_PATIENT = @idPaciente AND STATE = 'A';
END
GO

IF OBJECT_ID('sp_total_citas_pendientes', 'P') IS NOT NULL
    DROP PROCEDURE sp_total_citas_pendientes;
GO

CREATE PROCEDURE sp_total_citas_pendientes
    @idPaciente INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT COUNT(*) AS Citas_Pendientes 
    FROM TB_APPOINTMENTS
    WHERE ID_PATIENT = @idPaciente AND STATE = 'P';
END
GO

IF OBJECT_ID('sp_total_citas_canceladas', 'P') IS NOT NULL
    DROP PROCEDURE sp_total_citas_canceladas;
GO

CREATE PROCEDURE sp_total_citas_canceladas
    @idPaciente INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT COUNT(*) AS Citas_Canceladas 
    FROM TB_APPOINTMENTS
    WHERE ID_PATIENT = @idPaciente AND STATE = 'X';
END
GO

IF OBJECT_ID('sp_proximas_citas', 'P') IS NOT NULL
    DROP PROCEDURE sp_proximas_citas;
GO

CREATE PROCEDURE sp_proximas_citas
    @idPaciente INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        a.ID_APPOINTMENT,
        CONCAT(d.FIRST_NAME, ' ', d.LAST_NAME_PAT, ' ', d.LAST_NAME_MAT) AS Nombres_Completos,
        s.NAME_SPECIALTY,
        FORMAT(a.DATE_APPOINTMENT, 'HH:mm') AS Hora_Cita,
        FORMAT(a.DATE_APPOINTMENT, 'dd/MM/yyyy') AS Fecha_Cita,
        a.STATE,
        a.APPOINTMENT_PRICE,
        c.NUMBER_CONSULTORIES,
        c.FLOOR_NUMBER
    FROM TB_APPOINTMENTS a
    INNER JOIN TB_USERS d ON a.ID_DOCTOR = d.ID_USER
    INNER JOIN TB_SPECIALTIES s ON a.ID_SPECIALTY = s.ID_SPECIALTY
    INNER JOIN TB_CONSULTORIES c ON a.ID_CONSULTORIES = c.ID_CONSULTORIES
    WHERE a.ID_PATIENT = @idPaciente 
        AND a.DATE_APPOINTMENT >= CAST(GETDATE() AS DATE)
        AND a.STATE = 'P'
    ORDER BY a.DATE_APPOINTMENT ASC;
END
GO

exec sp_proximas_citas 2
GO

IF OBJECT_ID('sp_total_doctores', 'P') IS NOT NULL
    DROP PROCEDURE sp_total_doctores;
GO

CREATE PROCEDURE sp_total_doctores
AS
BEGIN
    SET NOCOUNT ON;

    SELECT COUNT(*) AS total_Doctores 
    FROM TB_USERS 
    WHERE ID_ROLE = 3 AND FLG_DELETE = 0;
END
GO

-- Core

IF OBJECT_ID('SP_LIST_SPECIALTIES_WITH_DESCRIPTION', 'P') IS NOT NULL
    DROP PROCEDURE SP_LIST_SPECIALTIES_WITH_DESCRIPTION;
GO

CREATE PROC SP_LIST_SPECIALTIES_WITH_DESCRIPTION
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        S.ID_SPECIALTY, 
        S.NAME_SPECIALTY, 
        S.DESCRIPTION_SPECIALITY,
        COUNT(DS.ID_DOCTOR) AS DoctorCount 
    FROM 
        TB_SPECIALTIES AS S
    LEFT JOIN 
        TB_DOCTOR_SPECIALTIES AS DS ON S.ID_SPECIALTY = DS.ID_SPECIALTY

    WHERE 
        S.FLG_DELETE = 0 -- Solo especialidades activas

    GROUP BY
        S.ID_SPECIALTY, 
        S.NAME_SPECIALTY, 
        S.DESCRIPTION_SPECIALITY 
    
    ORDER BY 
        S.NAME_SPECIALTY;
END
GO

IF OBJECT_ID('SP_LIST_DOCTORS_BY_SPECIALTIES', 'P') IS NOT NULL
    DROP PROCEDURE SP_LIST_DOCTORS_BY_SPECIALTIES;
GO

CREATE PROCEDURE SP_LIST_DOCTORS_BY_SPECIALTIES
    @IdSpecialty INT
AS
BEGIN
    SET NOCOUNT ON;
    
    DECLARE @Today DATETIME = CAST(GETDATE() AS DATE);
    DECLARE @Tomorrow DATETIME = DATEADD(DAY, 1, @Today);
    DECLARE @EndOfWeek DATETIME = DATEADD(DAY, 7, @Today);
    
    SELECT DISTINCT
        U.ID_USER AS IdDoctor,
        CONCAT(U.FIRST_NAME, ' ', U.LAST_NAME_PAT, ' ', U.LAST_NAME_MAT) AS FullName,
        S.ID_SPECIALTY,
        S.NAME_SPECIALTY,
        U.EMAIL,
        U.PHONE,
        U.PROFILE_PICTURE AS ProfilePicture,
        DS.YEARS_EXPERIENCE AS YearsExperience,
        DS.EXPERIENCE AS ExperienceDescription,
        DS.DOC_LANGUAGES AS Languages,
        -- Disponibilidad HOY
        (SELECT COUNT(*)
         FROM TB_DOCTOR_SCHEDULES DSS
         WHERE DSS.ID_DOCTOR = U.ID_USER
           AND CAST(DSS.FECHA_INICIO AS DATE) = @Today
           AND DSS.FECHA_FIN > GETDATE()
        ) AS AvailableToday,
        -- Disponibilidad MA�ANA
        (SELECT COUNT(*)
         FROM TB_DOCTOR_SCHEDULES DSS
         WHERE DSS.ID_DOCTOR = U.ID_USER
           AND CAST(DSS.FECHA_INICIO AS DATE) = @Tomorrow
        ) AS AvailableTomorrow,
        -- Disponibilidad ESTA SEMANA
        (SELECT COUNT(*)
         FROM TB_DOCTOR_SCHEDULES DSS
         WHERE DSS.ID_DOCTOR = U.ID_USER
           AND DSS.FECHA_INICIO >= @Today
           AND DSS.FECHA_INICIO < @EndOfWeek
        ) AS AvailableThisWeek,
        -- Etiqueta de disponibilidad
        CASE 
            WHEN EXISTS (
                SELECT 1 FROM TB_DOCTOR_SCHEDULES DSS
                WHERE DSS.ID_DOCTOR = U.ID_USER
                  AND CAST(DSS.FECHA_INICIO AS DATE) = @Today
                  AND DSS.FECHA_FIN > GETDATE()
            ) THEN 'Hoy'
            WHEN EXISTS (
                SELECT 1 FROM TB_DOCTOR_SCHEDULES DSS
                WHERE DSS.ID_DOCTOR = U.ID_USER
                  AND CAST(DSS.FECHA_INICIO AS DATE) = @Tomorrow
            ) THEN 'Mañana'
            WHEN EXISTS (
                SELECT 1 FROM TB_DOCTOR_SCHEDULES DSS
                WHERE DSS.ID_DOCTOR = U.ID_USER
                  AND DSS.FECHA_INICIO >= @Today
                  AND DSS.FECHA_INICIO < @EndOfWeek
            ) THEN 'Esta Semana'
            ELSE 'Sin Disponibilidad'
        END AS AvailableLabel
    FROM TB_USERS U
    INNER JOIN TB_DOCTOR_SPECIALTIES DS ON U.ID_USER = DS.ID_DOCTOR
    INNER JOIN TB_SPECIALTIES S ON DS.ID_SPECIALTY = S.ID_SPECIALTY
    WHERE DS.ID_SPECIALTY = @IdSpecialty
        AND U.ID_ROLE = 3  -- Solo doctores
        AND U.FLG_DELETE = 0
    ORDER BY DS.YEARS_EXPERIENCE DESC;
END
GO

IF OBJECT_ID('SP_LIST_DOCTOR_INFO', 'P') IS NOT NULL
    DROP PROCEDURE SP_LIST_DOCTOR_INFO;
GO

CREATE PROCEDURE SP_LIST_DOCTOR_INFO
    @IdDoctor INT = NULL
AS
BEGIN
    SET NOCOUNT ON;
    
    SELECT 
        U.ID_USER AS IdDoctor,
        CONCAT(U.FIRST_NAME, ' ', U.LAST_NAME_PAT, ' ', U.LAST_NAME_MAT) AS FullName,
        U.EMAIL,
        U.PHONE,
        U.PROFILE_PICTURE AS ProfilePicture,
        DS.ID_SPECIALTY AS IdSpecialty,
        S.NAME_SPECIALTY AS SpecialtyName
    FROM TB_USERS U
    INNER JOIN TB_DOCTOR_SPECIALTIES DS ON U.ID_USER = DS.ID_DOCTOR
    INNER JOIN TB_SPECIALTIES S ON DS.ID_SPECIALTY = S.ID_SPECIALTY
    WHERE U.ID_ROLE = 3 
        AND U.FLG_DELETE = 0
        AND S.FLG_DELETE = 0
        AND (@IdDoctor IS NULL OR U.ID_USER = @IdDoctor)
    ORDER BY U.LAST_NAME_PAT, U.LAST_NAME_MAT, U.FIRST_NAME;
END
GO

IF OBJECT_ID('SP_SEARCH_AVAILABLE_DATES_APPOINTMENTS', 'P') IS NOT NULL
    DROP PROCEDURE SP_SEARCH_AVAILABLE_DATES_APPOINTMENTS;
GO

-- Calendario de fechas disponibles/ocupadas
CREATE PROCEDURE SP_SEARCH_AVAILABLE_DATES_APPOINTMENTS
    @IdDoctor INT,
    @IdSpecialty INT,
    @StartDate DATE = NULL,
    @EndDate DATE = NULL
AS
BEGIN
    SET NOCOUNT ON;
    
    -- Si no se especifican fechas, buscar desde hoy hasta 30 d�as adelante
    IF @StartDate IS NULL
        SET @StartDate = CAST(GETDATE() AS DATE);
    
    IF @EndDate IS NULL
        SET @EndDate = DATEADD(DAY, 30, @StartDate);
    
    -- Tabla temporal para almacenar fechas disponibles
    CREATE TABLE #AvailableDates (
        ScheduleDate DATE,
        IdSchedule INT,
        IdConsultory INT,
        StartTime DATETIME,
        EndTime DATETIME,
        SlotDuration INT,
        TotalSlots INT,
        OccupiedSlots INT,
        AvailableSlots INT
    );
    
    -- Insertar horarios del doctor en el rango de fechas
    INSERT INTO #AvailableDates (ScheduleDate, IdSchedule, IdConsultory, StartTime, EndTime, SlotDuration, TotalSlots, OccupiedSlots)
    SELECT 
        CAST(DS.FECHA_INICIO AS DATE) AS ScheduleDate,
        DS.ID_SCHEDULE,
        DS.ID_CONSULTORIES,
        DS.FECHA_INICIO,
        DS.FECHA_FIN,
        DS.DURACION_CITA,
        FLOOR(DATEDIFF(MINUTE, DS.FECHA_INICIO, DS.FECHA_FIN) / DS.DURACION_CITA) AS TotalSlots,
        (SELECT COUNT(*) 
         FROM TB_APPOINTMENTS A
         WHERE A.ID_DOCTOR = @IdDoctor
           AND A.ID_SPECIALTY = @IdSpecialty
           AND A.DATE_APPOINTMENT >= DS.FECHA_INICIO
           AND A.DATE_APPOINTMENT < DS.FECHA_FIN
           AND A.STATE IN ('P', 'A')  -- Solo pendientes o atendidas
        ) AS OccupiedSlots
    FROM TB_DOCTOR_SCHEDULES DS
    WHERE DS.ID_DOCTOR = @IdDoctor
      AND CAST(DS.FECHA_INICIO AS DATE) BETWEEN @StartDate AND @EndDate
      AND DS.FECHA_FIN > GETDATE();  -- Solo horarios futuros
    
    -- Calcular slots disponibles
    UPDATE #AvailableDates
    SET AvailableSlots = TotalSlots - OccupiedSlots;
    
    -- Retornar solo fechas con disponibilidad
    SELECT 
        ScheduleDate,
        IdSchedule,
        IdConsultory,
        StartTime,
        EndTime,
        SlotDuration,
        TotalSlots,
        OccupiedSlots,
        AvailableSlots,
        DATENAME(WEEKDAY, ScheduleDate) AS DayOfWeek
    FROM #AvailableDates
    WHERE AvailableSlots > 0
    ORDER BY ScheduleDate, StartTime;
    
    DROP TABLE #AvailableDates;
END
GO

IF OBJECT_ID('SP_GET_AVAILABLE_TIME_SLOTS', 'P') IS NOT NULL
    DROP PROCEDURE SP_GET_AVAILABLE_TIME_SLOTS;
GO

-- Grilla de slots de horas disponibles/ocupadas
CREATE PROCEDURE SP_GET_AVAILABLE_TIME_SLOTS
    @IdDoctor INT,
    @IdSpecialty INT,
    @ScheduleDate DATE
AS
BEGIN
    SET NOCOUNT ON;
    
    -- Tabla temporal para horarios
    CREATE TABLE #TimeSlots (
        SlotTime DATETIME,
        IsAvailable BIT
    );
    
    -- Obtener horarios del doctor para esa fecha
    DECLARE @IdSchedule INT;
    DECLARE @StartTime DATETIME;
    DECLARE @EndTime DATETIME;
    DECLARE @SlotDuration INT;
    DECLARE @CurrentSlot DATETIME;
    
    SELECT TOP 1
        @IdSchedule = ID_SCHEDULE,
        @StartTime = FECHA_INICIO,
        @EndTime = FECHA_FIN,
        @SlotDuration = DURACION_CITA
    FROM TB_DOCTOR_SCHEDULES
    WHERE ID_DOCTOR = @IdDoctor
      AND CAST(FECHA_INICIO AS DATE) = @ScheduleDate
      AND FECHA_FIN > GETDATE()
    ORDER BY FECHA_INICIO;
    
    -- Si no hay horario, retornar vac�o
    IF @IdSchedule IS NULL
    BEGIN
        SELECT 
            'Sin horario disponible para esta fecha' AS Message;
        RETURN;
    END
    
    -- Generar todas las franjas horarias posibles
    SET @CurrentSlot = @StartTime;
    
    WHILE @CurrentSlot < @EndTime
    BEGIN
        INSERT INTO #TimeSlots (SlotTime, IsAvailable)
        VALUES (@CurrentSlot, 1);
        
        SET @CurrentSlot = DATEADD(MINUTE, @SlotDuration, @CurrentSlot);
    END
    
    -- Marcar como no disponibles las citas ya agendadas
    UPDATE TS
    SET TS.IsAvailable = 0
    FROM #TimeSlots TS
    WHERE EXISTS (
        SELECT 1 
        FROM TB_APPOINTMENTS A
        WHERE A.ID_DOCTOR = @IdDoctor
          AND A.ID_SPECIALTY = @IdSpecialty
          AND A.DATE_APPOINTMENT = TS.SlotTime
          AND A.STATE IN ('P', 'A')
    );
    
    -- Retornar franjas horarias
    SELECT 
        SlotTime,
        CONVERT(VARCHAR(5), SlotTime, 108) AS TimeSlot,
        IsAvailable,
        CASE WHEN IsAvailable = 1 THEN 'Disponible' ELSE 'Ocupado' END AS Status
    FROM #TimeSlots
    ORDER BY SlotTime;
    
    DROP TABLE #TimeSlots;
END
GO


IF OBJECT_ID('SP_VALIDATE_APPOINTMENT_AVAILABILITY', 'P') IS NOT NULL
    DROP PROCEDURE SP_VALIDATE_APPOINTMENT_AVAILABILITY;
GO

CREATE PROCEDURE SP_VALIDATE_APPOINTMENT_AVAILABILITY
    @IdPatient INT,
    @IdDoctor INT,
    @IdSpecialty INT,
    @DateAppointment DATETIME
AS
BEGIN
    SET NOCOUNT ON;
    
    DECLARE @IsAvailable BIT = 1;
    DECLARE @ErrorMessage VARCHAR(500) = '';
    
    -- 1. Validar que el horario del doctor exista para esa fecha/hora
    IF NOT EXISTS (
        SELECT 1 
        FROM TB_DOCTOR_SCHEDULES DS
        WHERE DS.ID_DOCTOR = @IdDoctor
          AND @DateAppointment >= DS.FECHA_INICIO
          AND @DateAppointment < DS.FECHA_FIN
    )
    BEGIN
        SET @IsAvailable = 0;
        SET @ErrorMessage = 'El doctor no tiene horario disponible en esa fecha/hora.';
    END
    
    -- 2. Validar que no exista otra cita del doctor a esa hora
    IF @IsAvailable = 1 AND EXISTS (
        SELECT 1 
        FROM TB_APPOINTMENTS A
        WHERE A.ID_DOCTOR = @IdDoctor
          AND A.DATE_APPOINTMENT = @DateAppointment
          AND A.STATE IN ('P', 'A')
    )
    BEGIN
        SET @IsAvailable = 0;
        SET @ErrorMessage = 'El doctor ya tiene una cita agendada a esa hora.';
    END
    
    -- 3. Validar que el paciente no tenga otra cita a esa misma hora
    IF @IsAvailable = 1 AND EXISTS (
        SELECT 1 
        FROM TB_APPOINTMENTS A
        WHERE A.ID_PATIENT = @IdPatient
          AND A.DATE_APPOINTMENT = @DateAppointment
          AND A.STATE IN ('P', 'A')
    )
    BEGIN
        SET @IsAvailable = 0;
        SET @ErrorMessage = 'Ya tienes una cita agendada a esa hora.';
    END
    
    -- 4. Validar que la fecha no sea en el pasado
    IF @IsAvailable = 1 AND @DateAppointment < GETDATE()
    BEGIN
        SET @IsAvailable = 0;
        SET @ErrorMessage = 'No se puede agendar citas en el pasado.';
    END
    
    -- Retornar resultado de validaci�n
    SELECT 
        @IsAvailable AS IsAvailable,
        @ErrorMessage AS ErrorMessage,
        @IdPatient AS IdPatient,
        @IdDoctor AS IdDoctor,
        @IdSpecialty AS IdSpecialty,
        @DateAppointment AS DateAppointment;
END
GO

IF OBJECT_ID('SP_CREATE_APPOINTMENT', 'P') IS NOT NULL
    DROP PROCEDURE SP_CREATE_APPOINTMENT;
GO

CREATE OR ALTER PROCEDURE SP_CREATE_APPOINTMENT
    @IdPatient INT,
    @IdDoctor INT,
    @IdSpecialty INT,
    @DateAppointment DATETIME,
    @AppointmentPrice DECIMAL(10,2) = 20.00
AS
BEGIN
    SET NOCOUNT ON;
    
    BEGIN TRY
      
        IF EXISTS (
            SELECT 1
            FROM TB_APPOINTMENTS
            WHERE ID_DOCTOR = @IdDoctor
            AND DATE_APPOINTMENT = @DateAppointment
            AND STATE IN ('P', 'A') -- P: Pendiente, A: Agendada. Excluye X: Cancelada
        )
        BEGIN
            -- Se lanza un error si el doctor ya tiene una cita agendada a esa hora.
            RAISERROR('La hora de la cita ya está ocupada por otra reserva. Por favor, seleccione otro horario.', 16, 1);
            RETURN;
        END

        -- 1.2 Validar que la hora de la cita caiga dentro del horario de trabajo definido.
        DECLARE @IdConsultory INT;
        
        SELECT TOP 1 @IdConsultory = ID_CONSULTORIES
        FROM TB_DOCTOR_SCHEDULES
        WHERE ID_DOCTOR = @IdDoctor
          AND @DateAppointment >= FECHA_INICIO
          AND @DateAppointment < FECHA_FIN;
        
        IF @IdConsultory IS NULL
        BEGIN
            -- Se lanza un error si el doctor no está trabajando a esa hora.
            RAISERROR('El doctor no tiene horario disponible para la fecha y hora seleccionada.', 16, 1);
            RETURN;
        END
        
        BEGIN TRANSACTION;
                
        -- Insertar la cita
        INSERT INTO TB_APPOINTMENTS (
            ID_PATIENT, 
            ID_DOCTOR, 
            ID_SPECIALTY, 
            DATE_APPOINTMENT, 
            ID_CONSULTORIES,
            STATE, 
            APPOINTMENT_PRICE
        )
        VALUES (
            @IdPatient,
            @IdDoctor,
            @IdSpecialty,
            @DateAppointment,
            @IdConsultory,
            'P',   -- Estado Pendiente
            @AppointmentPrice
        );
        
        SELECT SCOPE_IDENTITY() AS IdAppointment;
        
        COMMIT TRANSACTION;
    END TRY
    BEGIN CATCH
        IF @@TRANCOUNT > 0
            ROLLBACK TRANSACTION;
        
        SELECT 
            NULL AS IdAppointment,
            ERROR_MESSAGE() AS ErrorMessage,
            ERROR_NUMBER() AS ErrorNumber;
    END CATCH
END
GO

IF OBJECT_ID('SP_GET_APPOINTMENT_DETAILS', 'P') IS NOT NULL
    DROP PROCEDURE SP_GET_APPOINTMENT_DETAILS;
GO

CREATE PROCEDURE SP_GET_APPOINTMENT_DETAILS
    @IdAppointment INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        A.ID_APPOINTMENT,
        A.ID_PATIENT,
        CONCAT (P.FIRST_NAME, ' ', P.LAST_NAME_PAT, ' ', P.LAST_NAME_MAT) AS PatientFullName,
        A.ID_DOCTOR,
        CONCAT (U.FIRST_NAME, ' ', U.LAST_NAME_PAT, ' ', U.LAST_NAME_MAT) AS DoctorFullName,
        A.ID_SPECIALTY,
        S.NAME_SPECIALTY,
        A.DATE_APPOINTMENT,
        A.ID_CONSULTORIES,
        C.NUMBER_CONSULTORIES AS ConsultoryNumber,
        C.FLOOR_NUMBER AS FloorNumber,
        A.STATE,
        A.APPOINTMENT_PRICE
        FROM TB_APPOINTMENTS A
    INNER JOIN TB_USERS U ON A.ID_DOCTOR = U.ID_USER
    INNER JOIN TB_SPECIALTIES S ON A.ID_SPECIALTY = S.ID_SPECIALTY
    INNER JOIN TB_CONSULTORIES C ON A.ID_CONSULTORIES = C.ID_CONSULTORIES
    INNER JOIN TB_USERS P ON A.ID_PATIENT = P.ID_USER
    WHERE A.ID_APPOINTMENT = @IdAppointment;
    
    IF @@ROWCOUNT = 0
    BEGIN
        SELECT 
            NULL AS ID_APPOINTMENT,
            'Cita no encontrada' AS Message;
    END
END
GO

IF OBJECT_ID('SP_EDIT_APPOINTMENT', 'P') IS NOT NULL
    DROP PROCEDURE SP_EDIT_APPOINTMENT;

GO
CREATE PROCEDURE SP_EDIT_APPOINTMENT
    @IdAppointment INT,
    @IdDoctor INT,
    @IdSpecialty INT,
    @DateAppointment DATETIME,
    @IdConsultories INT,
    @State CHAR(1)
AS
BEGIN
    SET NOCOUNT ON;
    
    BEGIN TRY
        
        -- Realizar el UPDATE en la tabla TB_APPOINTMENTS
        UPDATE TB_APPOINTMENTS
        SET 
            ID_DOCTOR = @IdDoctor,
            ID_SPECIALTY = @IdSpecialty,
            DATE_APPOINTMENT = @DateAppointment,
            ID_CONSULTORIES = @IdConsultories,
            STATE = @State
        WHERE 
            ID_APPOINTMENT = @IdAppointment;
        
        -- Si la edici�n fue exitosa, retorna el ID de la cita editada
        SELECT @IdAppointment AS IdAppointmentEdited;
        
    END TRY
    BEGIN CATCH
        -- Manejo de errores (solo el ErrorMessage y ErrorNumber)
        SELECT 
            NULL AS IdAppointmentEdited,
            ERROR_MESSAGE() AS ErrorMessage,
            ERROR_NUMBER() AS ErrorNumber;
    END CATCH
END
GO

IF OBJECT_ID('SP_CANCEL_APPOINTMENT', 'P') IS NOT NULL
    DROP PROCEDURE SP_CANCEL_APPOINTMENT;

GO

CREATE OR ALTER PROCEDURE SP_CANCEL_APPOINTMENT
    @IdAppointment INT
AS
BEGIN
    SET NOCOUNT ON;
    
    BEGIN TRY
        
        UPDATE TB_APPOINTMENTS
        SET 
            STATE = 'X'
        WHERE 
            ID_APPOINTMENT = @IdAppointment;
        
        IF @@ROWCOUNT = 0
        BEGIN
            RAISERROR('No se encontró la cita con el ID especificado para cancelar.', 16, 1);
            RETURN;
        END

        SELECT @IdAppointment AS IdAppointmentCancelled;
        
    END TRY
    BEGIN CATCH
        SELECT 
            NULL AS IdAppointmentCancelled,
            ERROR_MESSAGE() AS ErrorMessage,
            ERROR_NUMBER() AS ErrorNumber;
    END CATCH
END
GO


SET DATEFORMAT YMD;

EXEC SP_LIST_SPECIALTIES_WITH_DESCRIPTION;

EXEC SP_LIST_DOCTORS_BY_SPECIALTIES @IdSpecialty = 3;

EXEC SP_LIST_DOCTOR_INFO @IdDoctor = 7;

EXEC SP_SEARCH_AVAILABLE_DATES_APPOINTMENTS 
    @IdDoctor = 7, 
    @IdSpecialty = 1;

EXEC SP_GET_AVAILABLE_TIME_SLOTS 
    @IdDoctor = 9, 
    @IdSpecialty = 2, 
    @ScheduleDate = '2025-10-28';


EXEC SP_VALIDATE_APPOINTMENT_AVAILABILITY
    @IdPatient = 5,
    @IdDoctor = 12,
    @IdSpecialty = 3,
    @DateAppointment = '2025-10-28 10:30:00';

EXEC SP_CREATE_APPOINTMENT
    @IdPatient = 4,                     
    @IdDoctor = 9,                     
    @IdSpecialty = 2,    
    @DateAppointment = '2025-10-28 11:20:00'


EXEC SP_GET_APPOINTMENT_DETAILS 33

EXEC SP_EDIT_APPOINTMENT
    @IdAppointment = 33,
    @IdDoctor = 7,              
    @IdSpecialty = 1,           
    @DateAppointment = '2025-10-27 11:20:00',
    @IdConsultories = 1,       
    @State = 'X';               

EXEC SP_CANCEL_APPOINTMENT 28

/* ============================================
   PROCEDURES MEDICAL RECORDS
   ============================================ */

IF OBJECT_ID('SP_CREATE_OR_UPDATE_MEDICAL_RECORD', 'P') IS NOT NULL
    DROP PROCEDURE SP_CREATE_OR_UPDATE_MEDICAL_RECORD;
GO

CREATE PROCEDURE SP_CREATE_OR_UPDATE_MEDICAL_RECORD
    @ID_APPOINTMENT INT,
    @OBSERVATIONS VARCHAR(2000),
    @DIAGNOSIS VARCHAR(500) = NULL,
    @TREATMENT VARCHAR(2000) = NULL
AS
BEGIN
    SET NOCOUNT ON;

    IF NOT EXISTS (SELECT 1 FROM TB_APPOINTMENTS WHERE ID_APPOINTMENT = @ID_APPOINTMENT)
    BEGIN
        RAISERROR('La cita con ID %d no existe', 16, 1, @ID_APPOINTMENT);
        RETURN;
    END

    IF NOT EXISTS (SELECT 1 FROM TB_APPOINTMENTS WHERE ID_APPOINTMENT = @ID_APPOINTMENT AND STATE = 'A')
    BEGIN
        RAISERROR('La cita debe estar en estado Atendida (A) para crear un registro m�dico', 16, 1);
        RETURN;
    END

    IF EXISTS (SELECT 1 FROM TB_MEDICAL_RECORDS WHERE ID_APPOINTMENT = @ID_APPOINTMENT)
    BEGIN
        UPDATE TB_MEDICAL_RECORDS
        SET 
            DATE_REPORT = GETDATE(),
            OBSERVATIONS = @OBSERVATIONS,
            DIAGNOSIS = @DIAGNOSIS,
            TREATMENT = @TREATMENT
        WHERE ID_APPOINTMENT = @ID_APPOINTMENT;

        SELECT 
            ID_RECORD,
            'Registro m�dico actualizado exitosamente' AS Mensaje
        FROM TB_MEDICAL_RECORDS
        WHERE ID_APPOINTMENT = @ID_APPOINTMENT;
    END
    ELSE
    BEGIN
        INSERT INTO TB_MEDICAL_RECORDS (ID_APPOINTMENT, OBSERVATIONS, DIAGNOSIS, TREATMENT)
        VALUES (@ID_APPOINTMENT, @OBSERVATIONS, @DIAGNOSIS, @TREATMENT);

        SELECT 
            SCOPE_IDENTITY() AS ID_RECORD,
            'Registro m�dico creado exitosamente' AS Mensaje;
    END
END
GO

IF OBJECT_ID('SP_GET_MEDICAL_RECORD_BY_APPOINTMENT', 'P') IS NOT NULL
    DROP PROCEDURE SP_GET_MEDICAL_RECORD_BY_APPOINTMENT;
GO

CREATE PROCEDURE SP_GET_MEDICAL_RECORD_BY_APPOINTMENT
    @ID_APPOINTMENT INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        MR.ID_RECORD,
        MR.ID_APPOINTMENT,
        MR.DATE_REPORT,
        MR.OBSERVATIONS,
        MR.DIAGNOSIS,
        MR.TREATMENT,
        A.ID_PATIENT,
        A.ID_DOCTOR,
        A.ID_SPECIALTY,
        A.DATE_APPOINTMENT,
        A.STATE,
        A.APPOINTMENT_PRICE
    FROM TB_MEDICAL_RECORDS MR
    INNER JOIN TB_APPOINTMENTS A ON MR.ID_APPOINTMENT = A.ID_APPOINTMENT
    WHERE MR.ID_APPOINTMENT = @ID_APPOINTMENT;
END
GO

IF OBJECT_ID('SP_GET_PATIENT_MEDICAL_HISTORY', 'P') IS NOT NULL
    DROP PROCEDURE SP_GET_PATIENT_MEDICAL_HISTORY;
GO

CREATE PROCEDURE SP_GET_PATIENT_MEDICAL_HISTORY
    @ID_PATIENT INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        MR.ID_RECORD,
        MR.ID_APPOINTMENT,
        MR.DATE_REPORT,
        MR.OBSERVATIONS,
        MR.DIAGNOSIS,
        MR.TREATMENT,
        A.DATE_APPOINTMENT,
        A.APPOINTMENT_PRICE,
        A.STATE AS AppointmentState,
        D.FIRST_NAME AS DoctorFirstName,
        D.LAST_NAME_PAT AS DoctorLastNamePat,
        D.LAST_NAME_MAT AS DoctorLastNameMat,
        S.NAME_SPECIALTY,
        C.NUMBER_CONSULTORIES
    FROM TB_MEDICAL_RECORDS MR
    INNER JOIN TB_APPOINTMENTS A ON MR.ID_APPOINTMENT = A.ID_APPOINTMENT
    INNER JOIN TB_USERS D ON A.ID_DOCTOR = D.ID_USER
    INNER JOIN TB_SPECIALTIES S ON A.ID_SPECIALTY = S.ID_SPECIALTY
    INNER JOIN TB_CONSULTORIES C ON A.ID_CONSULTORIES = C.ID_CONSULTORIES
    WHERE A.ID_PATIENT = @ID_PATIENT
    ORDER BY MR.DATE_REPORT DESC;
END
GO

/* ============================================
   PROCEDURES ADDITIONAL SERVICES
   ============================================ */

IF OBJECT_ID('SP_ADD_ADDITIONAL_SERVICE', 'P') IS NOT NULL
    DROP PROCEDURE SP_ADD_ADDITIONAL_SERVICE;
GO

CREATE PROCEDURE SP_ADD_ADDITIONAL_SERVICE
    @ID_RECORD INT,
    @ID_SERVICE INT
AS
BEGIN
    SET NOCOUNT ON;

    IF NOT EXISTS (SELECT 1 FROM TB_MEDICAL_RECORDS WHERE ID_RECORD = @ID_RECORD)
    BEGIN
        RAISERROR('El registro m�dico con ID %d no existe', 16, 1, @ID_RECORD);
        RETURN;
    END

    IF NOT EXISTS (SELECT 1 FROM TB_SERVICES WHERE ID_SERVICE = @ID_SERVICE AND FLG_DELETE = 0)
    BEGIN
        RAISERROR('El servicio con ID %d no existe o est� eliminado', 16, 1, @ID_SERVICE);
        RETURN;
    END

    IF EXISTS (SELECT 1 FROM TB_ADDITIONAL_SERVICES WHERE ID_RECORD = @ID_RECORD AND ID_SERVICE = @ID_SERVICE)
    BEGIN
        RAISERROR('El servicio ya est� asociado a este registro m�dico', 16, 1);
        RETURN;
    END

    INSERT INTO TB_ADDITIONAL_SERVICES (ID_RECORD, ID_SERVICE, STATE)
    VALUES (@ID_RECORD, @ID_SERVICE, 'P');

    SELECT 
        SCOPE_IDENTITY() AS ID_ADD_SERVICE,
        'Servicio adicional agregado exitosamente' AS Mensaje;
END
GO

IF OBJECT_ID('SP_LIST_ADDITIONAL_SERVICES_BY_RECORD', 'P') IS NOT NULL
    DROP PROCEDURE SP_LIST_ADDITIONAL_SERVICES_BY_RECORD;
GO

CREATE PROCEDURE SP_LIST_ADDITIONAL_SERVICES_BY_RECORD
    @ID_RECORD INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        ADS.ID_ADD_SERVICE,
        ADS.ID_RECORD,
        ADS.ID_SERVICE,
        ADS.STATE,
        S.NAME_SERVICE,
        S.DESCRIPTION,
        S.PRICE,
        S.DURATION_MINUTES,
        SPE.ID_SPECIALTY,
        SPE.NAME_SPECIALTY
    FROM TB_ADDITIONAL_SERVICES ADS
    INNER JOIN TB_SERVICES S ON ADS.ID_SERVICE = S.ID_SERVICE
    INNER JOIN TB_SPECIALTIES SPE ON S.ID_SPECIALTY = SPE.ID_SPECIALTY
    WHERE ADS.ID_RECORD = @ID_RECORD
    ORDER BY ADS.ID_ADD_SERVICE;
END
GO

IF OBJECT_ID('SP_UPDATE_ADDITIONAL_SERVICE_STATE', 'P') IS NOT NULL
    DROP PROCEDURE SP_UPDATE_ADDITIONAL_SERVICE_STATE;
GO

CREATE PROCEDURE SP_UPDATE_ADDITIONAL_SERVICE_STATE
    @ID_ADD_SERVICE INT,
    @STATE CHAR(1)
AS
BEGIN
    SET NOCOUNT ON;

    IF NOT EXISTS (SELECT 1 FROM TB_ADDITIONAL_SERVICES WHERE ID_ADD_SERVICE = @ID_ADD_SERVICE)
    BEGIN
        RAISERROR('El servicio adicional con ID %d no existe', 16, 1, @ID_ADD_SERVICE);
        RETURN;
    END

    IF @STATE NOT IN ('P', 'A', 'X')
    BEGIN
        RAISERROR('Estado inv�lido. Use: P=Pendiente, A=Aprobado, X=Cancelado', 16, 1);
        RETURN;
    END

    UPDATE TB_ADDITIONAL_SERVICES
    SET STATE = @STATE
    WHERE ID_ADD_SERVICE = @ID_ADD_SERVICE;

    SELECT 'Estado del servicio adicional actualizado exitosamente' AS Mensaje;
END
GO

IF OBJECT_ID('SP_CALCULATE_ADDITIONAL_SERVICES_TOTAL', 'P') IS NOT NULL
    DROP PROCEDURE SP_CALCULATE_ADDITIONAL_SERVICES_TOTAL;
GO

CREATE PROCEDURE SP_CALCULATE_ADDITIONAL_SERVICES_TOTAL
    @ID_RECORD INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        @ID_RECORD AS ID_RECORD,
        COUNT(*) AS TotalServices,
        ISNULL(SUM(S.PRICE), 0) AS TotalCost
    FROM TB_ADDITIONAL_SERVICES ADS
    INNER JOIN TB_SERVICES S ON ADS.ID_SERVICE = S.ID_SERVICE
    WHERE ADS.ID_RECORD = @ID_RECORD AND ADS.STATE != 'X';
END
GO

SET DATEFORMAT YMD;
