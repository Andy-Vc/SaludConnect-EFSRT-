USE BD_SALUDCONNECT;
GO
/* PROCEDURES SESSION */
CREATE OR ALTER PROC SP_LOGIN_USER
    @EMAIL VARCHAR(100),
    @PASSWORD VARCHAR(255)
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        U.ID_USER,
        U.FIRST_NAME,
        U.LAST_NAME_PAT,
        U.LAST_NAME_MAT,
		U.DOCUMENT,
        U.EMAIL,
		U.BIRTHDATE,
		U.GENDER,
		U.PHONE,
        U.ID_ROLE,
        R.NAME_ROLE
    FROM TB_USERS U
    INNER JOIN TB_ROLES R ON U.ID_ROLE = R.ID_ROLE
    WHERE U.EMAIL = @EMAIL AND U.PASSWORD_HASH = @PASSWORD AND U.FLG_DELETE = 0;

    IF EXISTS (
        SELECT 1
        FROM TB_USERS U
        WHERE U.EMAIL = @EMAIL AND U.PASSWORD_HASH = @PASSWORD AND U.FLG_DELETE = 0 AND U.ID_ROLE = 3
    )
    BEGIN
        SELECT 
            DS.ID_SPECIALTY,
            DS.YEARS_EXPERIENCE,
            S.NAME_SPECIALTY
        FROM TB_USERS U
        INNER JOIN TB_DOCTOR_SPECIALTIES DS ON U.ID_USER = DS.ID_DOCTOR
        INNER JOIN TB_SPECIALTIES S ON DS.ID_SPECIALTY = S.ID_SPECIALTY
        WHERE U.EMAIL = @EMAIL AND U.PASSWORD_HASH = @PASSWORD AND U.FLG_DELETE = 0;
    END
END
GO

CREATE OR ALTER PROC SP_REGISTER_PATIENTS
	@FIRST_NAME VARCHAR(50),
    @LAST_NAME_PAT VARCHAR(50),
    @LAST_NAME_MAT VARCHAR(50),
    @DOCUMENT VARCHAR(9),
	@BIRTHDATE DATE,
	@PHONE VARCHAR(13),
	@GENDER CHAR(1),
    @EMAIL VARCHAR(100),
    @PASSWORD_HASH VARCHAR(255)
AS
BEGIN
	IF EXISTS (SELECT 1 FROM TB_USERS WHERE DOCUMENT = @DOCUMENT)
    BEGIN
		RAISERROR('El número de documento %s ya existe', 16, 1, @DOCUMENT);
        RETURN;
    END
	IF EXISTS (SELECT 1 FROM TB_USERS WHERE EMAIL = @EMAIL)
    BEGIN
		RAISERROR('El correo electronico %s ya existe', 16, 1, @EMAIL);
        RETURN;
    END
	INSERT INTO TB_USERS(FIRST_NAME,LAST_NAME_PAT,LAST_NAME_MAT,DOCUMENT,BIRTHDATE,PHONE,GENDER,EMAIL,PASSWORD_HASH,ID_ROLE)
	VALUES(@FIRST_NAME,@LAST_NAME_PAT,@LAST_NAME_MAT,@DOCUMENT,@BIRTHDATE,@PHONE,@GENDER,@EMAIL,@PASSWORD_HASH,1)
END
GO
/* PROCEDURES SERVICES */
CREATE OR ALTER PROC SP_LIST_SERVICES_FOR_DOCTOR
	@IDDOCTOR INT
AS
BEGIN
	SELECT S.ID_SERVICE,NAME_SERVICE,S.DESCRIPTION,S.PRICE
	, S.DURATION_MINUTES, SPE.ID_SPECIALTY, SPE.NAME_SPECIALTY 
	FROM TB_SERVICES S
	JOIN TB_SPECIALTIES SPE ON S.ID_SPECIALTY = SPE.ID_SPECIALTY
	JOIN TB_DOCTOR_SPECIALTIES D ON D.ID_SPECIALTY = S.ID_SPECIALTY
	WHERE D.ID_DOCTOR = @IDDOCTOR
END
GO

CREATE OR ALTER PROC SP_TOTAL_SERVICES
AS
BEGIN
	SELECT COUNT(*) AS totalServices
	FROM TB_SERVICES WHERE FLG_DELETE = 0
END
GO

CREATE OR ALTER PROC SP_MIN_SERVICE
AS
BEGIN
	SELECT MIN(DURATION_MINUTES) AS MinDuration
	FROM TB_SERVICES
	WHERE FLG_DELETE = 0;
END
GO
/* PROCEDURES APPOINTMENTS */
CREATE OR ALTER PROC SP_APPOINTMENTS_TODAY_FOR_DOCTOR
	@IDDOCTOR INT
AS
BEGIN
    SELECT COUNT(*) AS AppointmentsToday
    FROM TB_APPOINTMENTS
    WHERE 
        ID_DOCTOR = @IDDOCTOR
        AND CAST(DATE_APPOINTMENT AS DATE) = CAST(GETDATE() AS DATE);
END
GO

CREATE OR ALTER PROC SP_TOTAL_PATIENTS_FOR_DOCTOR
	@IDDOCTOR INT
AS
BEGIN
	SELECT COUNT(DISTINCT ID_PATIENT) AS TotalUniquePatients
    FROM TB_APPOINTMENTS
    WHERE ID_DOCTOR = @IDDOCTOR
END
GO

CREATE OR ALTER PROC SP_TOTAL_APPOINTMENTS_COMPLETED
	@IDDOCTOR INT
AS
BEGIN
	SELECT COUNT(*) AS CompletedAppointments
    FROM TB_APPOINTMENTS
    WHERE ID_DOCTOR = @IDDOCTOR AND STATE = 'A';
END
GO

CREATE OR ALTER PROC SP_COUNT_UPCOMMING_APPOINTMENTS_FOR_DOCTOR
    @IDDOCTOR INT
AS
BEGIN
    SELECT COUNT(*) AS UpcomingAppointments
    FROM TB_APPOINTMENTS
    WHERE 
    ID_DOCTOR = @IDDOCTOR AND DATE_APPOINTMENT > GETDATE()
    AND STATE = 'P'; 
END
GO

CREATE OR ALTER PROC SP_LIST_APPOINTMENTS_BY_DATE_FOR_DOCTOR
    @DoctorId INT,
    @Date DATE = NULL
AS
BEGIN
    SELECT 
        A.ID_APPOINTMENT,
        P.ID_USER AS PatientId,
        P.FIRST_NAME,
        P.LAST_NAME_PAT,
        P.LAST_NAME_MAT,
        CAST(A.DATE_APPOINTMENT AS DATE) AS DateOnly,
        CONVERT(VARCHAR(5), A.DATE_APPOINTMENT, 108) AS AppointmentTime, 
        A.STATE,
        S.NAME_SERVICE
    FROM TB_APPOINTMENTS A
    INNER JOIN TB_USERS P ON A.ID_PATIENT = P.ID_USER
    INNER JOIN TB_SERVICES S ON A.ID_SERVICE = S.ID_SERVICE
    WHERE A.ID_DOCTOR = @DoctorId
      AND (@Date IS NULL OR CAST(A.DATE_APPOINTMENT AS DATE) = @Date)
    ORDER BY A.DATE_APPOINTMENT
END
GO

CREATE OR ALTER PROC SP_GET_APPOINTMENTS_FOR_7_DAYS
	@DoctorId INT
AS
BEGIN
	SET NOCOUNT ON;

	SELECT 
		CAST(DATE_APPOINTMENT AS DATE) AS AppointmentDate,
		COUNT(*) AS TotalAppointments
	FROM TB_APPOINTMENTS
	WHERE 
		DATE_APPOINTMENT >= CAST(GETDATE() - 6 AS DATE) 
		AND DATE_APPOINTMENT < CAST(GETDATE() + 1 AS DATE) 
		AND STATE IN ('A', 'P', 'X')
		AND (@DoctorId IS NULL OR ID_DOCTOR = @DoctorId)
	GROUP BY 
		CAST(DATE_APPOINTMENT AS DATE)
	ORDER BY 
		AppointmentDate;
END;
GO

CREATE OR ALTER PROC SP_GET_APPOINTEMNT_FOR_ID
	@ID_APPOINTMENT INT
AS
BEGIN
	SELECT 
        A.ID_APPOINTMENT,
        P.ID_USER AS PatientId,
        P.FIRST_NAME,
        P.LAST_NAME_PAT,
		P.DOCUMENT,
		P.BIRTHDATE,
		P.PHONE,
        P.LAST_NAME_MAT,
        CAST(A.DATE_APPOINTMENT AS DATE) AS DateOnly,
        CONVERT(VARCHAR(5), A.DATE_APPOINTMENT, 108) AS AppointmentTime, 
        A.STATE,
        S.NAME_SERVICE,
		S.PRICE
    FROM TB_APPOINTMENTS A
    INNER JOIN TB_USERS P ON A.ID_PATIENT = P.ID_USER
    INNER JOIN TB_SERVICES S ON A.ID_SERVICE = S.ID_SERVICE
    WHERE A.ID_APPOINTMENT = @ID_APPOINTMENT
END
GO

CREATE OR ALTER PROC SP_APPOINTMENT_CHANGE_STATE
	@IDAPPOINTMENT INT,
	@STATE CHAR(1)
AS
BEGIN
	UPDATE TB_APPOINTMENTS
	SET STATE = @STATE
	WHERE ID_APPOINTMENT = @IDAPPOINTMENT
END
GO
/* PROCEDURES SPECIALTIES */
CREATE OR ALTER PROC SP_TOTAL_SPECIALTIES
AS
BEGIN
	SELECT COUNT(*) AS totalSpecialties
	FROM TB_SPECIALTIES WHERE FLG_DELETE = 0
END
GO

/* PROCEDURES DOCTOR */
CREATE OR ALTER PROC SP_TOTAL_DOCTOR
AS
BEGIN
	SELECT COUNT(*) AS totalDoctors
	FROM TB_USERS
	WHERE ID_ROLE = 3 AND FLG_DELETE = 0
END
GO

/* PROCEDURES USER */
CREATE OR ALTER PROC SP_GET_USER_PROFILE
    @ID_USER INT
AS
BEGIN
    SET NOCOUNT ON;
    SELECT 
        U.ID_USER,
        U.FIRST_NAME,
        U.LAST_NAME_PAT,
        U.LAST_NAME_MAT,
        U.DOCUMENT,
        U.BIRTHDATE,
        U.PHONE,
        U.GENDER,
        U.EMAIL,
        U.ID_ROLE,
        R.NAME_ROLE
    FROM TB_USERS U
    INNER JOIN TB_ROLES R ON U.ID_ROLE = R.ID_ROLE
    WHERE U.ID_USER = @ID_USER AND U.FLG_DELETE = 0;
    IF EXISTS (SELECT 1 FROM TB_USERS WHERE ID_USER = @ID_USER AND ID_ROLE = 3 AND FLG_DELETE = 0)
    BEGIN
        SELECT 
            DS.ID_SPECIALTY,
            S.NAME_SPECIALTY,
            DS.YEARS_EXPERIENCE
        FROM TB_DOCTOR_SPECIALTIES DS
        INNER JOIN TB_SPECIALTIES S ON DS.ID_SPECIALTY = S.ID_SPECIALTY
        WHERE DS.ID_DOCTOR = @ID_USER;
    END
END
GO
